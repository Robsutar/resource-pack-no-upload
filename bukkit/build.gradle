plugins {
    alias(libs.plugins.shadow)
}

base {
    archivesName = project.archives_base_name
}

repositories {
    maven {
        name 'papermc'
        url 'https://repo.papermc.io/repository/maven-public/'
    }
}

dependencies {
    implementation project(':core')

    compileOnly 'org.jetbrains:annotations:23.0.0'

    // Paper is used just as transcriber of the bukkit classes, Paper classes are not used.
    // Version 1.15.2 is used for the same reason, is just an transcriber, newer version classes are not used.
    compileOnly 'com.destroystokyo.paper:paper-api:1.15.2-R0.1-SNAPSHOT'
    compileOnly "net.kyori:adventure-text-serializer-legacy:4.20.0"
    compileOnly "net.kyori:adventure-text-serializer-gson:4.20.0"
}

processResources {
    def props = [NAME: rootProject.name, VERSION: version, PACKAGE: project.group.toString()]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

shadowJar {
    def shadowPath = project.group.toString() + '.shadow.'

    archiveClassifier.set('')
    relocate 'io.netty', shadowPath + 'io.netty'
    minimize()
}

// Disable jar and replace with shadowJar
jar.enabled = false
assemble.dependsOn(shadowJar)
