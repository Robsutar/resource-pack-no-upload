plugins {
    id 'java'
    id 'com.gradleup.shadow' version '8.3.6'
}

group = 'com.robsutar.rnu'
version = '1.3.5'

compileJava.options.encoding = 'UTF-8'

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

repositories {
    mavenCentral()

    maven {
        name 'papermc'
        url 'https://repo.papermc.io/repository/maven-public/'
    }
}

dependencies {
    implementation 'io.netty:netty-all:4.1.115.Final'
    compileOnly 'org.jetbrains:annotations:23.0.0'
    // Paper is used just as transcriber of the bukkit classes, Paper classes are not used.
    // Version 1.15.2 is used for the same reason, is just an transcriber, newer version classes are not used.
    compileOnly 'com.destroystokyo.paper:paper-api:1.15.2-R0.1-SNAPSHOT'
}

processResources {
    def props = [NAME: rootProject.name, VERSION: version, PACKAGE: project.group.toString()]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

shadowJar {
    def shadowPath = project.group.toString() + '.shadow.'

    archiveClassifier.set('')
    relocate 'io.netty', shadowPath + 'io.netty'
    minimize()
}

// Disable jar and replace with shadowJar
jar.enabled = false
assemble.dependsOn(shadowJar)
